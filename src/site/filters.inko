import builder.html (Document)
import markdown.html (Filter)
import std.bytes (Slice)
import std.mime (Mime)
import std.set (Set)

type DeletedText {}

impl Filter for DeletedText {
  fn pub mut run(document: mut Document) {
    let nodes = document.nodes.iter_mut.to_array

    loop {
      match nodes.pop {
        case Some(Element(el)) -> {
          match el.attributes.get('class') {
            case Ok('del') if el.name == 'span' -> {
              el.attributes = Map.new
              el.name = 'del'
            }
            case _ -> {}
          }

          nodes.append(el.nodes.iter_mut.to_array)
        }
        case Some(_) -> {}
        case _ -> break
      }
    }
  }
}

type Videos {
  let @formats: Set[Slice[String]]

  fn static new -> Self {
    Self(Set.from_array(['mp4'.to_slice, 'webm'.to_slice]))
  }
}

impl Filter for Videos {
  fn pub mut run(document: mut Document) {
    let nodes = document.nodes.iter_mut.to_array

    loop {
      match nodes.pop {
        case Some(Element(img)) if img.name == 'img' -> {
          let path = img.attributes.get('src').or('')
          let mime = match path.split('.').last {
            case Some(ext) if @formats.contains?(ext) -> {
              match Mime.from_extension(ext) {
                case Some(v) -> v
                case _ -> return
              }
            }
            case _ -> next
          }

          img.name = 'video'
          img.attr('controls', 'true').attr('preload', 'metadata').attr(
            'autoplay',
            'false',
          )

          let _ = img.attributes.remove('src')
          let _ = img.attributes.remove('alt')

          img.source.attr('src', path).attr('type', mime.to_string)
        }
        case Some(Element(v)) -> nodes.append(v.nodes.iter_mut.to_array)
        case Some(_) -> {}
        case _ -> break
      }
    }
  }
}

type LazyImages {
  fn static new -> Self {
    Self()
  }
}

impl Filter for LazyImages {
  fn pub mut run(document: mut Document) {
    let nodes = document.nodes.iter_mut.to_array

    loop {
      match nodes.pop {
        case Some(Element(v)) if v.name == 'img' -> v.attr('loading', 'lazy')
        case Some(Element(v)) -> nodes.append(v.nodes.iter_mut.to_array)
        case Some(_) -> {}
        case _ -> break
      }
    }
  }
}
